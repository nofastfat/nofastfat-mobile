<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml"
                layout="absolute"
                addedToStage="added()"
                xmlns:ui="com.xy.view.ui.*"
                width="100%"
                resize="application1_resizeHandler(event)"
                height="100%"
                xmlns:panels="com.xy.view.ui.panels.*">
    <mx:Style>
		Alert{
			font-size:12px;
		}
	</mx:Style>
    <mx:Script>
        <![CDATA[
            import com.InvoicingFacade;
            import com.xy.model.Config;
            import com.xy.util.EnterFrameCall;
            import com.xy.util.STool;
            import com.xy.view.ui.ContentUI;
            import com.xy.view.ui.MessageUI;

            import mx.events.FlexEvent;
            import mx.events.ResizeEvent;
            import mx.managers.PopUpManager;

            private var _contentUI : ContentUI;

            private var _facade : InvoicingFacade;

            private function added() : void {
                stage.scaleMode = StageScaleMode.NO_SCALE;
                stage.align = StageAlign.TOP_LEFT;
                application1_resizeHandler();

                Config.HTTP_URL = parameters["httpUrl"];

                EnterFrameCall.initState(stage);

                _facade = new InvoicingFacade();
                _facade.startUp(this);

                ProgressUI.init(this, progressUI);
                STool.remove(progressUI);
            }

            protected function application1_resizeHandler(event : ResizeEvent = null) : void {
                if (stage == null) {
                    return;
                }

                var sh : int = stage.stageHeight;
                var sw : int = stage.stageWidth;

                if (leftMenuUI != null) {
                    var h : int = sh - leftMenuUI.y - 10;
                    h = Math.max(h, 464);
                    leftMenuUI.height = h;
                }

                if (topUI != null) {
                    topUI.width = sw - topUI.x - 10;
                }

                if (_contentUI != null) {
                    _contentUI.x = leftMenuUI.x + leftMenuUI.width + 10;
                    _contentUI.y = topUI.y + topUI.height + 10;
                    var w : int = sw - _contentUI.x - 10;
                    w = Math.max(w, _contentUI.getMinWidth());

                    h = sh - _contentUI.y - 10;
                    h = Math.max(h, _contentUI.getMinHeight());

                    _contentUI.width = w;
                    _contentUI.height = h;
                }
            }

            public function setContent(contentUI : ContentUI) : void {
                if (_contentUI == contentUI) {
                    return;
                }
                if (_contentUI != null) {
                    STool.remove(_contentUI);
                }

                _contentUI = contentUI;

                this.addChild(_contentUI);

                application1_resizeHandler();
            }
        ]]>
    </mx:Script>
    <ui:TopUI left="10"
              id="topUI"
              top="10"
              right="10"
              height="74">
    </ui:TopUI>
    <ui:LeftMenuUI x="10"
                   id="leftMenuUI"
                   y="92">
    </ui:LeftMenuUI>
    <ui:ProgressUI x="179"
                   y="10"
                   id="progressUI">
    </ui:ProgressUI>

</mx:Application>

